plugins {
	id 'java'
	id 'io.quarkus'
	id 'io.freefair.lombok' version '8.11'
	id 'org.liquibase.gradle' version '2.2.0'
	id 'com.diffplug.spotless' version '7.2.1'
	id 'org.sonarqube' version '7.0.1.6134'
	id 'jacoco'
}

repositories {
	mavenLocal()
	mavenCentral()
}
// how to enable assert keyword on JVM Quarkus
dependencies {
	implementation 'io.quarkiverse.cucumber:quarkus-cucumber:1.3.0'
	implementation 'io.quarkus:quarkus-rest-jackson'
	implementation 'io.quarkus:quarkus-smallrye-openapi'
	implementation 'io.quarkus:quarkus-vertx'
	implementation 'io.quarkus:quarkus-hibernate-validator'
	implementation 'io.quarkus:quarkus-junit5-mockito'
	implementation 'io.quarkus:quarkus-hibernate-orm-panache'
	implementation 'io.quarkus:quarkus-jdbc-postgresql'
	implementation 'io.quarkus:quarkus-liquibase'
	implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
	implementation 'io.quarkus:quarkus-arc'
	implementation 'at.favre.lib:bcrypt:0.10.2'
	testImplementation 'io.quarkus:quarkus-junit5'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'org.skyscreamer:jsonassert:1.5.1'
	testImplementation 'org.assertj:assertj-core:3.27.6'
	liquibaseRuntime 'org.liquibase:liquibase-core:4.24.0'
	liquibaseRuntime 'org.postgresql:postgresql:42.7.3'
	liquibaseRuntime 'info.picocli:picocli:4.7.5'
	implementation 'io.quarkus:quarkus-smallrye-jwt'
	implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
}

group 'br.com.bagnascojhoel'
version 'latest'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

test {
	systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
compileJava {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-parameters'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

liquibase {
	activities {
		main {
			url "jdbc:postgresql://localhost:5432/kwik_ecommerce"
			username "kwik"
			password "kwik"
		}
	}
}

quarkusDev {
	jvmArgs = ["-enableassertions"]
}

spotless {
	ratchetFrom('main')

	gherkin {
		target 'src/**/*.feature' // you have to set the target manually
		gherkinUtils() // has its own section below
	}

	java {
		importOrder()
		removeUnusedImports()
		removeWildcardImports()
		googleJavaFormat()
	}

	groovy {
		target '*.gradle'

		importOrder()
		removeSemicolons()
		greclipse()
	}

	flexmark {

		target 'README.md', 'docs/**/*.md' // you have to set the target manually
		flexmark() // or flexmark('0.64.8') // version is optional
	}

	sql {
		target 'src/main/resources/**/*.sql' // have to set manually
	}

	json {
		target 'src/**/*.json'
		jackson()
				.feature('INDENT_OUTPUT', true)
				.feature('ORDER_MAP_ENTRIES_BY_KEYS', true)
	}
}

tasks.getByName('jacocoTestReport') {
	dependsOn 'test' // tests are required to run before generating the report
	reports {
		xml.required.set(true)
		csv.required.set(false)
		html.outputLocation.set(layout.buildDirectory.dir('jacocoHtml'))
	}
}

tasks.getByName('sonar') {
	dependsOn 'jacocoTestReport' // make sure the report is generated before analysis
}

sonar {
	properties {
		// Prefer external configuration (CI or local env vars) for sensitive values.
		property 'sonar.organization', project.findProperty('sonarOrganization') ?: System.getenv('SONAR_ORGANIZATION') ?: 'replace-with-your-org'
		// Use SonarCloud host
		property 'sonar.host.url', 'https://sonarcloud.io'
		property 'sonar.projectKey', project.findProperty('sonarProjectKey') ?: System.getenv('SONAR_PROJECT_KEY') ?: 'replace-with-your-project-key'
		property 'sonar.projectName', 'kwik-ecommerce-quarkus'
		property 'sonar.sources', 'src/main'
		property 'sonar.tests', 'src/test'
		// property 'sonar.java.binaries', 'build/classes/java/main'
		// property 'sonar.java.test.binaries', 'build/classes/java/test'
		// Use environment variable or Gradle property for authentication (do not commit tokens to repo)
		property 'sonar.login', project.findProperty('sonarLogin') ?: System.getenv('SONAR_TOKEN')
		property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
	}
}

tasks.register('format') {
	dependsOn 'compileJava'
	dependsOn 'spotlessApply'
}

tasks.register('testIntegration', Test) {
	group = 'verification'
	description = 'Runs integration tests with optional feature file or scenario name filters'

	def featureFile = project.findProperty('featureFile') ?: ''
	def scenarioName = project.findProperty('scenarioName') ?: ''

	systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"

	include '**/*IntegrationTest.class'

	if (featureFile) {
		systemProperty 'cucumber.features', featureFile
	}

	if (scenarioName) {
		systemProperty 'cucumber.filter.name', scenarioName
	}
}

tasks.register('testE2e', Test) {
	group = 'verification'
	description = 'Runs E2E tests with optional feature file or scenario name filters'

	def featureFile = project.findProperty('featureFile') ?: ''
	def scenarioName = project.findProperty('scenarioName') ?: ''

	systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"

	include '**/*E2eTest.class'

	if (featureFile) {
		systemProperty 'cucumber.features', featureFile
	}

	if (scenarioName) {
		systemProperty 'cucumber.filter.name', scenarioName
	}
}

tasks.register('testUnit', Test) {
	group = 'verification'
	description = 'Runs unit tests with optional class name filter'

	def className = project.findProperty('className') ?: ''

	systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"

	// Exclude Cucumber test runners, only include unit tests
	include '**/*Test.class'
	exclude '**/*IntegrationTest.class', '**/*E2eTest.class'

	if (className) {
		include "**/${className}.class"
	}
}
